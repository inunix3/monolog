int WIDTH = 20;
int HEIGHT = 10;
int available = WIDTH * HEIGHT;

void error() {
    println("Error");
    exit(-1);
}

void reserve([[int]] auditorium, int x, int y) {
    if (auditorium[y][x]) {
        println("Already reserved!");

        return;
    }

    auditorium[y][x] = 1;
    --available;
}

void unreserve([[int]] auditorium, int x, int y) {
    if (!auditorium[y][x]) {
        println("The queried sit is not reserved.");

        return;
    }

    auditorium[y][x] = 0;
    ++available;
}

void unreserve_all([[int]] auditorium) {
    for (int i = 0; i < HEIGHT; ++i) {
        for (int j = 0; j < WIDTH; ++j) {
            if (auditorium[i][j]) {
                auditorium[i][j] = 0;
                ++available;
            }
        }
    }
}

void draw_auditorium([[int]] auditorium) {
    for (int i = 0; i < HEIGHT; ++i) {
        for (int j = 0; j < WIDTH; ++j) {
            if (auditorium[i][j])  {
                print("X");
            } else {
                print("O");
            }
        }

        println("");
    }
}

string prompt_string(string what) {
    print(what);

    string? input = input_string();

    if (input == nil) {
        error();
    }

    return *input;
}

int prompt_int(string what) {
    print(what);

    int? input = input_int();

    if (input == nil) {
        error();
    }

    return *input;
}

[[int], HEIGHT] auditorium;

for (int i = 0; i < #auditorium; ++i) {
    auditorium[i] #= WIDTH;
}

println("Welcome to Cinema City's reservation program");

for (;;) {
    println("");
    println("1. Display sits");
    println("2. Reserve sit");
    println("3. Unreserve sit");
    println("4. Unreserve all sits");
    println("5. Print available sits");
    println("6. Exit");
    println("");

    string choice = prompt_string("> ");

    if (choice == "1") {
        draw_auditorium(auditorium);
    } else if (choice == "2") {
        int y = prompt_int("Vertical coord: ");
        int x = prompt_int("Horizontal coord: ");

        if (y < 0 || y >= HEIGHT || x < 0 || x >= WIDTH) {
            println("Bad coord!");

            continue;
        }

        reserve(auditorium, x, y);
    } else if (choice == "3") {
        int y = prompt_int("Vertical coord: ");
        int x = prompt_int("Horizontal coord: ");

        if (y < 0 || y >= HEIGHT || x < 0 || x >= WIDTH) {
            println("Bad coord!");

            continue;
        }

        unreserve(auditorium, x, y);
    } else if (choice == "4") {
        unreserve_all(auditorium);
    } else if (choice == "5") {
        println("There is available " + $available + " sits");
    } else if (choice == "6") {
        break;
    } else {
        println("Bad choice");
    }
}

